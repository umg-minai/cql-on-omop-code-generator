(cl:in-package #:model-info-generator)

(defmethod canonical-concept-column ((table table))
  (if (equal (name table) "concept")
      (find-column "concept_id" table)
      (let ((table-name (name table))
            (candidates '()))
        (mapc (lambda (column)
                (let ((name   (name column))
                      (suffix "_concept_id"))
                  (when (a:ends-with-subseq suffix name)
                    (let ((stem (subseq name 0 (- (length name)
                                                  (length suffix)))))
                      (push (cons stem column) candidates)))))
              (columns table))
        (labels ((for-name (name)
                   (a:assoc-value candidates name :test #'string=))
                 (try-suffix (suffix)
                   (when (a:ends-with-subseq suffix table-name)
                     (for-name (subseq table-name 0 (- (length table-name)
                                                       (length suffix)))))))
          (or (for-name table-name)
              (try-suffix "_occurrence")
              (try-suffix "_exposure"))))))
